$noop(★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★)
$noop(★                        MBP-SBP  Tagger Script                        ★)
$noop(★                             version  1.0                             ★)
$noop(★ Inspired by: MBP YA Magic-Script - v2.7                              ★)
$noop(★                                                                      ★)
$noop(★ Here is MY script for music renaming in MusicBrainz Picard           ★)
$noop(★                                                                      ★)
$noop(★ Note: Destination Director Setting                                   ★)
$noop(★       /Music                                                         ★)
$noop(★                                                                      ★)
$noop(★----------------------------------------------------------------------★)
$noop(★   Based on the MBP YA Magic-Script  by  xstefen                      ★)
$noop(★ > https://gist.github.com/xstefen/820c9b9e8cbe668a2d01d2ff60c69d24   ★)
$noop(★----------------------------------------------------------------------★)
$noop(★   -which is- Based on the MBP Magic Script  By Ski-lleR              ★)
$noop(★   Thanks to avibrazil for his filter                                 ★)
$noop(★ > https://github.com/avibrazil/picard-scripting                      ★)
$noop(★----------------------------------------------------------------------★)
$noop(★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★)

$noop(★========================MAIN FOLDER SEPARATORS========================★)
$noop(*                        *** Default Folder ***                        ★)
$set(_musicDirectory,Artists/)
$set(_isMusic,1)
$set(_isMainFolder,1)
$noop(*                         *** Compilations ***                         ★)
$noop(★ Referred as global compilation, as opposed to compilation            ★)
$noop(★ This is the %compilation% tag; seen as 'Compilation iTunes'          ★)
$noop(★ There is a release type tag called 'compilation'; which is different ★)
$noop(★ This will mostly have Various Artists as album artists               ★)
$if($eq(%compilation%,1),$set(_isGlobalCompilation,1)$delete(_isMainFolder))
$set(_globalCompilationDirectory,Compilations/)
$noop(*                         *** Sound Tracks ***                         ★)
$if($in(%releasetype%,soundtrack),$set(_isSoundtrack,1)$delete(_isMainFolder))
$set(_soundtrackDirectory,Soundtracks/)
$noop(*                          *** Audiobooks ***                          ★)
$if($in(%releasetype%,audiobook),$set(_isAudiobook,1)$delete(_isMainFolder)$delete(_isMusic))
$set(_audiobooksDirectory,Audiobooks/)
$noop(*                           *** Podcasts ***                           ★)
$if($in(%genre%,Podcast),$set(_isPodcast,1)$delete(_isMusic))
$set(_podcastDirectory,Podcasts/)
$noop(★======================================================================★)

$noop(★==================SUB FOLDER SEPARATORS BY MUSIC TYPE=================★)
$noop(★ Sub-folder within separations to classify releases                   ★)
$set(_variousDirectory,Various Artists/)
$noop(*                          *** Main Types ***                          ★)
$if($in(%releasetype%,album),$set(_isAlbum,1))
$set(_albumSubDirectory,)
$if($in(%releasetype%,single),$set(_isSingle,1))
$set(_singleSubDirectory,Singles/)
$if($in(%releasetype%,ep),$set(_isEp,1))
$set(_epSubDirectory,EPs/)
$if($in(%releasetype%,broadcast),$set(_isBroadcast,1))
$set(_broadcastSubDirectory,Broadcasts/)
$if($in(%releasetype%,other),$set(_isOther,1))
$set(_otherSubDirectory,Other/)
$noop(*                       *** Secondary  Types ***                       ★)
$if($in(%releasetype%,dj-mix),$set(_isDjmix,1))
$set(_djmixSubDirectory,DJ Mix/)
$if($in(%releasetype%,remix),$set(_isRemix,1))
$set(_remixSubDirectory,Remixes/)
$if($in(%releasetype%,live),$set(_isLive,1))
$set(_liveSubDirectory,Live/)
$if($in(%releasetype%,mixtape),$set(_isMixtape,1))
$if($in(%releasetype%,street),$set(_isMixtape,1))
$if($in(%releasetype%,demo),$set(_isMixtape,1))
$set(_mixtapeSubDirectory,Mixtapes/)
$if($in(%releasetype%,interview),$set(_isInterview,1))
$set(_interviewSubDirectory,Interviews/)
$if($in(%releasetype%,compilation),$set(_isCompilation,1))
$set(_compilationSubDirectory,Compilations/)
$noop(*                           *** No Types ***                           ★)
$if($in(%coverTributeSort%,Cover),$set(_isCover,1))
$set(_coverSubDirectory,Covers/)
$if($in(%coverTributeSort%,Tribute),$set(_isTribute,1))
$set(_tributeSubDirectory,Tributes/)
$if($eq($is_video(),1),$set(_isVideo,1))
$set(_videoSubDirectory,Videos/)
$if($in(%releasestatus%,bootleg),$set(_isUnofficial,1))
$set(_unofficialDirectory,Bootleg/)
$noop(★======================================================================★)
$if($eq(%album%,[non-album tracks]),$set(_isStandalone,1))
$noop(★=========================FILE NAMING PREAMBLE=========================★)
$noop(★ Drop Unused Placeholder Tags                                         ★)
$if($eq(%title%,Title),$delete(title)$delete(titlesort))
$if($eq(%subtitle%,Subtitle),$delete(subtitle))
$if($eq(%discsubtitle%,DiscSubtitle),$delete(discsubtitle))
$if($eq(%artists%,Artist;AdditionalArtists),$delete(artists))
$if($eq(%artistssortorder%,Artist),$delete(artistssortorder))
$if($eq(%album%,Album),$delete(album)$delete(albumsort)$delete(albumartistsort)$delete(albumartist))
$if($eq(%media%,Media),$delete(media))
$if($eq(%label%,Label),$delete(label))
$if($eq(%catalognumber%,Cat#),$delete(catalognumber))
$noop(★ TYPOGRAPHY: So that these names will not contain operators           ★)
$noop(★ ... ➡ …                                                              *)
$set(album,$replace(%album%,...,…))
$set(title,$replace(%title%,...,…))
$set(discsubtitle,$replace(%discsubtitle%,...,…))
$noop(★ No. ➡ №                                                              *)
$set(album,$replace(%album%, [Nn]o.\\s*\(\\d\), №\\1))
$set(title,$rreplace(%title%, [Nn]o.\\s*\(\\d\), №\\1))
$set(discsubtitle,$replace(%discsubtitle%, [Nn]o.\\s*\(\\d\), №\\1))
$noop(★ [digit]" ➡ [digit]″ for Android and Samba                            *)
$set(discsubtitle,$rreplace(%discsubtitle%,\(\\d\)",\\1''))
$noop(★ "12" Vinyl" ➡ "12 Inch Vinyl" for Android and Samba                  *)
$set(media,$rreplace(%media%,\(\\d\)",\\1 Inch))
$set(album,$rreplace(%album%,\(\\d\)",\\1 Inch))
$set(title,$rreplace(%title%,\(\\d\)",\\1 Inch))
$noop(★ "something" ➡ “something” for android and samba                      *)
$set(albumartist,$rreplace(%albumartist%,"\(.*?\)",“\\1”))
$set(artist,$rreplace(%artist%,"\(.*?\)",“\\1”))
$set(albumartistsort,$rreplace(%albumartistsort%,"\(.*?\)",“\\1”))
$set(artistsort,$rreplace(%artistsort%,"\(.*?\)",“\\1”))
$set(album,$rreplace(%album%,"\(.*?\)",“\\1”))
$set(title,$rreplace(%title%,"\(.*?\)",“\\1”))
$set(discsubtitle,$rreplace(%discsubtitle%,"\(.*?\)",“\\1”))
$noop(★ Stary collecting naming info                                         ★)
$set(_titleForFilename,$if2(%titlesort%,%title%))
$set(_albumForFilename,$if2(%albumsort%,%album%))
$set(_albumartistForFilename,$if2(%albumartist%,%artist%))
$set(_albumartistsortForFilename,$if2(%albumartistsort%,%artistsort%,%albumartist%,%artist%))
$set(_artistForFilename,%artist%)
$set(_artistsortForFilename,$if2(%artistsort%,%artist%))
$set(_discsubtitleForFilename,%discsubtitle%)
$set(_labelForFilename,%label%)
$set(_trackLength,%_length%)
$noop(★ Get first initial from this info                                     ★)
$set(_dirTitle,%_albumartistForFilename%)
$if($eq($lower($left(%_dirTitle%,4)),the ),$set(_dirTitle,%_albumartistsortForFilename%))
$if($eq($lower($left(%_dirTitle%,2)),a ),$set(_dirTitle,%_albumartistsortForFilename%))
$set(_dirInitial,$firstalphachar(%_dirTitle%,1))
$noop(★ # ➡ ♯ : Android cannot handle #                                      *)
$set(_titleForFilename,$replace(%_titleForFilename%,#,♯))
$set(_albumForFilename,$replace(%_albumForFilename%,#,♯))
$set(_labelForFilename,$replace(%_labelForFilename%,#,♯))
$set(_discsubtitleForFilename,$replace(%_discsubtitleForFilename%,#,♯))
$noop(★ : ➡ ∶ : Android can't handle :                                       *)
$set(_trackLength,$replace(%_trackLength%,:,∶))
$set(_labelForFilename,$replace(%_labelForFilename%,:,∶))
$set(_titleForFilename,$replace(%_titleForFilename%,:,∶))
$set(_albumForFilename,$replace(%_albumForFilename%,:,∶))
$set(_discsubtitleForFilename,$replace(%_discsubtitleForFilename%,:,∶))
$set(_artistForFilename,$replace(%_artistForFilename%,:,∶))
$set(_albumartistForFilename,$replace(%_albumartistForFilename%,:,∶))
$set(_artistsortForFilename,$replace(%_artistsortForFilename%,:,∶))
$set(_albumartistsortForFilename,$replace(%_albumartistsortForFilename%,:,∶))
$noop(★ ? ➡ ⁇ : Android can't handle ?"                                      *)
$set(_titleForFilename,$replace(%_titleForFilename%,?,⁇))
$set(_albumForFilename,$replace(%_albumForFilename%,?,⁇))
$set(_discsubtitleForFilename,$replace(%_discsubtitleForFilename%,?,⁇))
$set(_artistForFilename,$replace(%_artistForFilename%,?,⁇))
$set(_artistsortForFilename,$replace(%_artistsortForFilename%,?,⁇))
$set(_labelForFilename,$replace(%_labelForFilename%,?,⁇))
$noop(★ | ➡ ￨ : Android can't handle |"                                      *)
$set(_titleForFilename,$replace(%_titleForFilename%,|,￨))
$set(_albumForFilename,$replace(%_albumForFilename%,|,￨))
$set(_discsubtitleForFilename,$replace(%_discsubtitleForFilename%,|,￨))
$set(_labelForFilename,$replace(%_labelForFilename%,|,￨))
$noop(★ > ➡ ＞: Android can't handle >"                                      *)
$set(_titleForFilename,$replace(%_titleForFilename%,>,＞))
$set(_albumForFilename,$replace(%_albumForFilename%,>,＞))
$set(_discsubtitleForFilename,$replace(%_discsubtitleForFilename%,>,＞))
$set(_labelForFilename,$replace(%_labelForFilename%,>,＞))
$noop(★ < ➡ ＜: Android can't handle <"                                      *)
$set(_titleForFilename,$replace(%_titleForFilename%,<,＜))
$set(_albumForFilename,$replace(%_albumForFilename%,<,＜))
$set(_discsubtitleForFilename,$replace(%_discsubtitleForFilename%,<,＜))
$set(_labelForFilename,$replace(%_labelForFilename%,<,＜))
$noop(★ * ➡ ✱ : Android can't handle *"                                      *)
$set(_titleForFilename,$replace(%_titleForFilename%,*,✱))
$set(_albumForFilename,$replace(%_albumForFilename%,*,✱))
$set(_discsubtitleForFilename,$replace(%_discsubtitleForFilename%,*,✱))
$set(_artistForFilename,$replace(%_artistForFilename%,*,✱))
$set(_albumartistForFilename,$replace(%_albumartistForFilename%,*,✱))
$set(_artistsortForFilename,$replace(%_artistsortForFilename%,*,✱))
$set(_albumartistsortForFilename,$replace(%_albumartistsortForFilename%,*,✱))
$set(_labelForFilename,$replace(%_labelForFilename%,*,✱))
$noop(★ & ➡ ＆:Android can't handle & but don't want the weird & for folders *)
$set(_titleForFilename,$replace(%_titleForFilename%,&,＆))
$set(_discsubtitleForFilename,$replace(%_discsubtitleForFilename%,&,＆))
$set(_artistForFilename,$replace(%_artistForFilename%,&,＆))
$set(_artistsortForFilename,$replace(%_artistsortForFilename%,&,＆))
$set(_albumForFilename,$replace(%_albumForFilename%,&,and))
$set(_albumartistForFilename,$replace(%_albumartistForFilename%,&,and))
$set(_albumartistsortForFilename,$replace(%_albumartistsortForFilename%,&,and))
$set(_labelForFilename,$replace(%_labelForFilename%,&,and))
$noop(★ / ➡ ⌿ : Can't use slashes for filenames                              *)
$set(_trackLength,$replace(%_trackLength%,/,⌿))
$set(_titleForFilename,$replace(%_titleForFilename%,/,⌿))
$set(_albumForFilename,$replace(%_albumForFilename%,/,⌿))
$set(_discsubtitleForFilename,$replace(%_discsubtitleForFilename%,/,⌿))
$set(_artistForFilename,$replace(%_artistForFilename%,/,⌿))
$set(_albumartistForFilename,$replace(%_albumartistForFilename%,/,⌿))
$set(_artistsortForFilename,$replace(%_artistsortForFilename%,/,⌿))
$set(_albumartistsortForFilename,$replace(%_albumartistsortForFilename%,/,⌿))
$set(_labelForFilename,$replace(%_labelForFilename%,/,⌿))
$noop(★ \\➡ ⍀ : Don't use backslashes for filenames                          *)
$set(_trackLength,$replace(%_trackLength%,\\,⍀))
$set(_titleForFilename,$replace(%_titleForFilename%,\\,⍀))
$set(_albumForFilename,$replace(%_albumForFilename%,\\,⍀))
$set(_discsubtitleForFilename,$replace(%_discsubtitleForFilename%,\\,⍀))
$set(_artistForFilename,$replace(%_artistForFilename%,\\,⍀))
$set(_albumartistForFilename,$replace(%_albumartistForFilename%,\\,⍀))
$set(_artistsortForFilename,$replace(%_artistsortForFilename%,\\,⍀))
$set(_albumartistsortForFilename,$replace(%_albumartistsortForFilename%,\\,⍀))
$set(_labelForFilename,$replace(%_labelForFilename%,\\,⍀))
$noop(★======================================================================★)

$noop(★================================NAMING================================★)
$noop(* Initial type directory                                               *)
$noop(★ [Artists/Compilations/Soundtracks/Podcasts/Audiobooks]               ★)
$if2($if(%_isAudiobook%,%_audiobooksDirectory%),$if(%_isPodcast%,%_podcastDirectory%),$if(%_isSoundtrack%,%_soundtrackDirectory%),$if(%_isGlobalCompilation%,%_globalCompilationDirectory%),%_musicDirectory%)
$noop(* If in a music directory, create initial subfolder                    *)
$if(%_isMusic%,[%_dirInitial%]/)
$noop(★ Create an album artist folder if the type is music                   ★)
$set(_divisionFolder,%_albumartistForFilename%)
$if($eq(%_albumartistForFilename%,Various Artists),$if(%_labelForFilename%,$set(_divisionFolder,%_labelForFilename%)))
$if(%_isMusic%,%_divisionFolder%/)
$noop(★ Pick out subtype, and put in that sub-directory                      ★)
$if(%_isMusic%,$if2($if(%_isDjmix%,%_djmixSubDirectory%),$if(%_isRemix%,%_remixSubDirectory%),$if(%_isUnofficial%,%_unofficialDirectory%),$if(%_isLive%,%_liveSubDirectory%),$if(%_isMixtape%,%_mixtapeSubDirectory%),$if(%_isInterview%,%_interviewSubDirectory%),$if($not(%_isGlobalCompilation%),$if(%_isCompilation%,%_compilationSubDirectory%)),$if(%_isCover%,%_coverSubDirectory%),$if(%_isTribute%,%_tributeSubDirectory%),$if(%_isVideo%,%_videoSubDirectory%),$if(%_isOther%,%_otherSubDirectory%),$if(%_isEp%,%_epSubDirectory%),$if(%_isSingle%,%_singleSubDirectory%),$if(%_isAlbum%,%_albumSubDirectory%),))
$noop(★ Album name formatting; put in date if exists.                        ★)
$if(%_isStandalone%,,$if(%_isMusic%,$if($if2(%originaldate%,%date%,%originalyear%,%year%),[$if2(%originaldate%,%date%,%originalyear%,%year%)] )))
$if(%_isStandalone%,,$if(%_isMusic%,%_albumForFilename%))
$if(%_isStandalone%,,$if(%_isMusic%,/))
$noop(★ Song name formatting                                                 ★)
$if(%_isStandalone%,,$if(%_isMusic%,$if($in(%media%,Vinyl),%_musicbrainz_tracknumber%,$if($gt(%totaldiscs%,1),%discnumber%-)$num(%tracknumber%,2)). ))
$if(%_isStandalone%,,$if(%_isMusic%,$if(%_trackLength%,[%_trackLength%] )))
$if(%_isMusic%,%_titleForFilename%)
$if(%_isMusic%, - %_artistForFilename%)
$noop(★                                                                      ★)
$noop(★ PODCASTS : Create an artist folder if the type is podcasts           ★)
$set(_pDate,$if2(%originaldate%,%date%,%originalyear%,%year%))
$if(%_isPodcast%,%_artistForFilename%/$if(%_pDate%,[%_pDate%] )%_titleForFilename%)
$noop(★ AUDIOBOOKS : Will configure this at a point                          ★)
$noop(★======================================================================★)
